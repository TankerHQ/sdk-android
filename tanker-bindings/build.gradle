apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'digital.wup.android-maven-publish'
apply from: 'native.gradle'

group 'io.tanker'
version 'dev'


android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30

        consumerProguardFiles 'proguard-rules.pro'
        ndk {
            moduleName "tanker-bindings-jni"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        testInstrumentationRunnerArguments (System.env.findAll { k, v -> k.startsWith('TANKER_') })
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
    }
    // This is the only way to build instrumented tests in release mode
    // https://stackoverflow.com/a/51719271/1810193
    if (project.hasProperty('androidTestRelease')) {
        testBuildType 'release'
        buildTypes {
            release {
                multiDexEnabled true
            }
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        pickFirst 'META-INF/tanker-bindings_release.kotlin_module'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
            systemProperty "jna.library.path", projectDir.absolutePath + "/conan/host"
        }
    }

    sourceSets.main {
        java.srcDirs += 'src/main/kotlin'
        java.excludes += 'src/main/kotlin/io/tanker/admin/**'
    }

    sourceSets.test {
        java.srcDirs += 'src/test/kotlin'
    }
    sourceSets.androidTest {
        java.srcDirs += ['src/test/java', 'src/test/kotlin']
    }

    sourceSets.debug {
        jni.srcDirs = []
        jniLibs.srcDir 'src/debug/jniLibs'
    }

    sourceSets.release {
        jni.srcDirs = []
        jniLibs.srcDir 'src/release/jniLibs'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javaDocJar(type: Jar, dependsOn: dokka) {
    from dokka.outputDirectory
    archiveClassifier.set('javadoc')
}

dokka {
    outputFormat = 'javadoc'

    // Exclude JNA R class
    packageOptions {
        prefix = "com.sun.jna"
        suppress = true
    }

    // Exclude our R class
    packageOptions {
        prefix = "io.tanker.tanker_bindings"
        suppress = true
    }

    // Exclude low-level JNA bindings
    packageOptions {
        prefix = "io.tanker.bindings"
        suppress = true
    }

    packageOptions {
        prefix = "io.tanker.admin"
        suppress = true
    }

    // Exclude Identity api
    packageOptions {
        prefix = "io.tanker.api.Identity"
        suppress = true
    }
}

// We use afterEvaluate to give the Android Gradle plugin time to inject the Adhoc Component 'release'
afterEvaluate {
    publishing {
        publications {
            tankerBindings(MavenPublication) {
                // Instead of components.android, we want only the release aar, otherwise publish
                // fails with "multiple artifacts with the identical extension and classifier",
                // because the debug and release .aar have no "classifier" to distinguish them
                from components.release

                // These have different classifiers (doc and source) so no conflict here
                artifact javaDocJar
                artifact sourcesJar
            }
        }

        repositories {
            maven {
                name = 'maven.tanker.io'
                url = 'gcs://maven.tanker.io'
            }
        }
    }
}


dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.1'
    api 'net.java.dev.jna:jna:5.4.0@aar'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.7.1")
    testImplementation "androidx.test:runner:1.3.0"
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "androidx.test.ext:junit:1.1.2"
    testImplementation "androidx.test:rules:1.3.0"
    // assertj 3.0 is not compatible with API level < 26
    testImplementation "org.assertj:assertj-core:2.9.1"

    testImplementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.0.pr3'
    testImplementation 'org.slf4j:slf4j-nop:1.7.28'
    testImplementation files('jna.jar')
    testImplementation files('conan/host/tanker-native.jar')

    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:rules:1.3.0"
    // assertj 3.0 is not compatible with API level < 26
    androidTestImplementation "org.assertj:assertj-core:2.9.1"

    androidTestImplementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.0.pr3'
    androidTestImplementation 'org.slf4j:slf4j-nop:1.7.28'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:2.0.0'
    }
}

apply plugin: 'com.getkeepsafe.dexcount'
